load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("@rules_python//python:packaging.bzl", "py_wheel")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@py_soabi//:soabi.bzl", "SOABI", "PLATFORM", "ABI")  # Now this load will succeed

config_setting(
    name = "arm64",
    constraint_values = ["@platforms//cpu:arm64"],
)

config_setting(
    name = "aarch64",
    constraint_values = ["@platforms//cpu:aarch64"],
)

selects.config_setting_group(
    name = "arm64_or_aarch64",
    match_any = [
        ":arm64",
        ":aarch64",
    ],
)


genrule(
  name = "hockeymom_ext",
  srcs = ["//hockeymom/src:_hockeymom.so"],
  outs = ["_hockeymom.%s.so" % SOABI],
  cmd = "cp $(location //hockeymom/src:_hockeymom.so) $@",
  visibility = ["//visibility:public"],
)

py_binary(
    name = "link_ext",
    srcs = ["scripts/link_ext.py"],
    data = [":hockeymom_ext"],
    args = [SOABI],
    tags = ["manual"],
)

py_wheel(
    name = "hockeymom_wheel",
    distribution = "hockeymom",
    version = "0.1.0",
    python_tag = ABI,
    abi = ABI,
    platform = PLATFORM,
    tags = ["python-wheel", "manual"],
    deps = [
        ":hockeymom_ext",
        ":hockeymom",
        "@enblend-enfuse//:enblend"
    ] + select({
        ":arm64_or_aarch64": [],
        "//conditions:default": ["@multiblend//:multiblend"],
    }),
)

py_library(
  name = "hockeymom",
  data = [":hockeymom_ext"],
  srcs = [
    "__init__.py",
    "core.py",
  ],
  imports = [".."],
  visibility = ["//visibility:public"],
)

py_binary(
    name = "bdist_wheel",
    srcs = ["scripts/bdist_wheel.py"],
    data = [":hockeymom_wheel"],
    tags = ["python-wheel", "manual"],
)

# Local Doxygen documentation for hockeymom C++/CUDA & pybind layer
genrule(
  name = "doxygen_docs",
  srcs = ["Doxyfile"] + glob(["csrc/**/*.h", "csrc/**/*.hpp", "csrc/**/*.cpp", "src/**/*.h", "src/**/*.cpp", "*.py"]),
  cmd = "if command -v doxygen >/dev/null 2>&1; then doxygen $(location Doxyfile) && tar -czf $@ -C doxygen_out html xml; else echo 'doxygen missing' > $@; fi",
  outs = ["hockeymom_doxygen_docs.tar.gz"],
  visibility = ["//visibility:public"],
  tags = ["manual"],
)
